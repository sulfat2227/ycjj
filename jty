#include <Keypad.h>
#include <Wire.h>
#include <LiquidCrystal_I2C.h>

#define LED_TRIGGER 7
#define LDR_PIN 10
#define BUZZER A3
#define LED_INDICATOR 6
#define LED_ALARM 8

LiquidCrystal_I2C lcd(0x27, 16, 2);

// Keypad
const byte ROWS = 4;
const byte COLS = 4;
char keys[ROWS][COLS] = {
  {'1','2','3','A'},
  {'4','5','6','B'},
  {'7','8','9','C'},
  {'*','0','#','D'}
};
byte rowPins[ROWS] = {2, 3, 4, 5};
byte colPins[COLS] = {A1, A2, A4, A5};
Keypad keypad = Keypad(makeKeymap(keys), rowPins, colPins, ROWS, COLS);

// PIN Keamanan
String correctPIN = "1234";
String enteredPIN = "";

bool alarmActive = true;

void setup() {
  pinMode(LED_TRIGGER, OUTPUT);
  pinMode(LDR_PIN, INPUT);
  pinMode(BUZZER, OUTPUT);
  pinMode(LED_INDICATOR, OUTPUT);
  pinMode(LED_ALARM, OUTPUT);

  digitalWrite(LED_TRIGGER, HIGH);   // LED pemicu nyala
  digitalWrite(LED_INDICATOR, HIGH); // LED rumah nyala terus

  lcd.init();
  lcd.backlight();
  lcd.setCursor(0,0);
  lcd.print("Sistem Alarm ON");
  lcd.setCursor(0,1);
  lcd.print("Masukkan PIN");
}

void loop() {
  char key = keypad.getKey();

  // Baca input keypad
  if (key) {
    if (key == '#') {
      if (enteredPIN == correctPIN) {
        alarmActive = !alarmActive;
        digitalWrite(BUZZER, LOW);
        if (alarmActive) {
          lcd.clear();
          lcd.setCursor(0,0);
          lcd.print("Alarm AKTIF");
        } else {
          lcd.clear();
          lcd.setCursor(0,0);
          lcd.print("Alarm NONAKTIF");
        }
      } else {
        lcd.clear();
        lcd.setCursor(0,0);
        lcd.print("PIN SALAH!");
      }
      enteredPIN = ""; // reset input
    } 
    else if (key == '*') {
      enteredPIN = ""; // hapus input PIN
      lcd.setCursor(0,1);
      lcd.print("PIN Dihapus    ");
    }
    else {
      enteredPIN += key;
      lcd.setCursor(0,1);
      lcd.print("PIN: ");
      for (int i=0; i<enteredPIN.length(); i++) {
        lcd.print("*"); // tampilkan bintang
      }
    }
  }

  int sensorValue = digitalRead(LDR_PIN);

  if (alarmActive) {
    digitalWrite(LED_ALARM, HIGH);

    if (sensorValue == HIGH) {
      digitalWrite(BUZZER, HIGH);
      blinkAlarmLED();
      lcd.setCursor(0,1);
      lcd.print("!! INTRUDER !!  ");
    } else {
      digitalWrite(BUZZER, LOW);
      lcd.setCursor(0,1);
      lcd.print("Area Aman       ");
    }
  } else {
    digitalWrite(LED_ALARM, LOW);
    digitalWrite(BUZZER, LOW);
  }
}

void blinkAlarmLED() {
  digitalWrite(LED_ALARM, HIGH);
  delay(100);
  digitalWrite(LED_ALARM, LOW);
  delay(100);
}
